// https://github.com/camunda/jenkins-global-shared-library
// https://github.com/camunda/cambpm-jenkins-shared-library
@Library(['camunda-ci', 'cambpm-jenkins-shared-library@INFRA-2114-fix-agent-disconnect']) _

/**
 * A method to perform a custom evaluation if certain stages in the Side track pipeline
 * should be evaluated. This is necessary since the Side Track pipeline triggers stage types
 * from the Main and Daily pipelines.
 *
 * Note: this method shouldn't be extracted to the shared library
 *       since it is specific to this pipeline.
 *
 * @param stageType - a String indicating the type of stage to be executed.
 * @return boolean. TRUE, if the stage should be triggered. Otherwise, FALSE.
 */
boolean shouldTrigger(String stageType) {
  def qaStages = ['sql-scripts', 'upgrade-database', 'instance-migration', 'old-engine', 'rolling-update', 'large-data-tests']
  def unitStages = ['engine-unit', 'engine-unit-authorizations', 'webapp-unit', 'webapp-unit-authorizations']
  if (params.STANDALONE) {
    // Daily run
    return qaStages.contains(stageType)
  } else {
    if (env.BRANCH_NAME == cambpmDefaultBranch()) {
      // Downstream master run
      return unitStages.contains(stageType)
    } else {
      // Downstream PR run
      return cambpmWithLabels(cambpmGetLabels(stageType))
    }
  }
}

pipeline {
  agent {
    node {
      label 'jenkins-job-runner'
    }
  }
  parameters {
    booleanParam name: 'STANDALONE', defaultValue: true
    booleanParam name: 'UPSTREAM_PR', defaultValue: false, description: 'Is the current build triggered in the scope of an upstream pull request'
    string name: 'UPSTREAM_PROJECT_NAME', defaultValue: 'cambpm-ce/cambpm-main/master'
    string name: 'UPSTREAM_BUILD_NUMBER', defaultValue: ''
    string name: 'PR_LABELS', defaultValue: '', description: 'The labels assigned to the CE pull request'
  }
  options {
    buildDiscarder(logRotator(numToKeepStr: '5'))
  }
  triggers {
    pollSCM('H H(0-7) * * *')
  }
  stages {
    stage('Prepare') {
      when {
        expression {
          !cambpmWithLabels('standalone')
        }
      }
      steps {
        cambpmConditionalRetry([
          agentLabel: 'centos-stable',
          runSteps: {
            cambpmCopyArtifacts(params.UPSTREAM_PROJECT_NAME, params.UPSTREAM_BUILD_NUMBER)
            cambpmStash("platform-stash-runtime",
                        ".m2/org/camunda/**/*-SNAPSHOT/**",
                        "**/qa/**,**/*qa*/**,**/*.zip,**/*.tar.gz")
            cambpmStash("platform-stash-qa",
                        ".m2/org/camunda/bpm/**/qa/**/*-SNAPSHOT/**,.m2/org/camunda/bpm/**/*qa*/**/*-SNAPSHOT/**",
                        "**/*.zip,**/*.tar.gz")
          }
        ])
      }
    }
    stage('UNIT DB tests') {
      matrix {
        axes {
          axis {
            name 'DB'
            values 'cockroachdb_201'
          }
          axis {
            name 'STAGE_TYPE'
            values 'engine-unit', 'engine-unit-authorizations', 'webapp-unit', 'webapp-unit-authorizations', 'sql-scripts', 'upgrade-database', 'instance-migration', 'old-engine', 'rolling-update', 'large-data-tests'
          }
        }
        when {
          // execute after agent is created
          expression {
            shouldTrigger(env.STAGE_TYPE)
          }
        }
        stages {
          stage('UNIT test') {
            steps {
              cambpmConditionalRetry([
                agentLabel: env.DB,
                runSteps: {
                  echo("UNIT DB Test Stage: ${env.STAGE_TYPE}-${env.DB}")
                  cambpmRunMavenByStageType(env.STAGE_TYPE, env.DB)
                },
                postAlways: {
                  script {
                    if (!env.STAGE_TYPE.equals('sql-scripts')) {
                      cambpmPublishTestResult();
                    }
                  }
                }
              ])
            }
          }
        }
      }
    }
  }
  post {
    changed {
      script {
        if (!agentDisconnected()){
          cambpmSendEmailNotification()
        }
      }
    }
    always {
      // Retrigger the build if the agent of jenkins-job-runner disconnected.
      script {
        if (agentDisconnected()) {
          build job: currentBuild.projectName, propagate: false, quietPeriod: 60, wait: false
        }
      }
    }
  }
}
